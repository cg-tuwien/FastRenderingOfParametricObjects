#version 460
#extension GL_EXT_shader_16bit_storage                   : require
#extension GL_EXT_shader_8bit_storage                    : require
#extension GL_EXT_nonuniform_qualifier                   : require
#extension GL_EXT_shader_image_int64                     : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive                   : enable
#extension GL_ARB_shader_draw_parameters                 : require
#extension GL_KHR_shader_subgroup_arithmetic             : require
#extension GL_KHR_shader_subgroup_ballot                 : require
#extension GL_KHR_shader_subgroup_shuffle                : require
#extension GL_EXT_debug_printf                           : enable

layout(local_size_x = 8, local_size_y = 4, local_size_z = 1) in;

#include "../shader_includes/host_device_shared.h"
#include "../shader_includes/util/ui64_conv.glsl"
#include "../shader_includes/util/glsl_helpers.glsl"
#include "../shader_includes/types.glsl"
#include "../shader_includes/common_ubo.glsl"

// +------------------------------------------------------------------------------+
// |   Bound Resources                                                            |
// +------------------------------------------------------------------------------+
// layout(set = 0, binding = 0) buffer is contained within common_ubo.glsl
layout(set = 1, binding = 0) buffer SsboCounters { uint mCounters[4]; } uCounters;
layout(set = 2, binding = 0) buffer ObjectData   { object_data mElements[]; }  uObjectData;
layout(set = 2, binding = 1) buffer PxFillParams { px_fill_data mElements[]; } uPxFillParams; 
layout(set = 2, binding = 2) buffer PxFillCount  { VkDrawIndirectCommand mDrawParams[]; } uPxFillCounts;
layout(set = 3, binding = 0) buffer PatchLodPing { px_fill_data mElements[]; } uPatchLodPing; // initial target
//layout(set = 3, binding = 1) buffer PatchLodPong { px_fill_data mElements[]; } uPatchLodPong; // unused in this whole shader
layout(set = 3, binding = 2) buffer PatchLodCount { uvec4 mDispatchCounts[]; } uPatchLodCount; 

#include "../shader_includes/parametric_functions/shape_functions.glsl"

// +------------------------------------------------------------------------------+
// |   PushConstants                                                              |
// +------------------------------------------------------------------------------+

layout(push_constant) uniform PushConstants
{
    uint mObjectId;
}
pushConstants;

// ################## compute shader main ###################
void main()
{
	const uint   objectId          = pushConstants.mObjectId;
	const uint   glyphId           = gl_WorkGroupID.x;
	const uvec2  datasetDimensions = uObjectData.mElements[objectId].mDetailEvalDims.zw;

	// in:
	const vec2 paramsStart = vec2(uObjectData.mElements[objectId].mParams[0], uObjectData.mElements[objectId].mParams[2]);
	const vec2 paramsEnd   = vec2(uObjectData.mElements[objectId].mParams[1], uObjectData.mElements[objectId].mParams[3]);
	const vec2 paramsRange = paramsEnd - paramsStart;

	const uvec2 evalDims = clamp(uObjectData.mElements[objectId].mDetailEvalDims.xy, uvec2(1, 1), gl_WorkGroupSize.xy);

	if (gl_LocalInvocationID.x >= evalDims.x || gl_LocalInvocationID.y >= evalDims.y) {
		return; // Nothing to do for this invocation
	}

	const vec2 rangePerEvalBucket = paramsRange / vec2(evalDims);
	const vec2 paramsFrom = paramsStart + rangePerEvalBucket * vec2(gl_LocalInvocationID.xy);
	const vec2 paramsTo   = paramsFrom + rangePerEvalBucket;

	// out:
	const uint insertIdx = atomicAdd(uPatchLodCount.mDispatchCounts[0].x, 1); // start with the first, a.k.a. "firstPing"
	uPatchLodPing.mElements[insertIdx].mParams           = vec4(paramsFrom[0], paramsTo[0], paramsFrom[1], paramsTo[1]);
	uPatchLodPing.mElements[insertIdx].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);


#if SPAWN_FIXER_PATCHES
	// If enabled: Just spawn six more patches---three around each pole:
	if (gl_LocalInvocationID.x == 0 && gl_LocalInvocationID.y == 0)
	{
		const uint insertIdxXtra = atomicAdd(uPatchLodCount.mDispatchCounts[0].x, 6); // start with the first, a.k.a. "firstPing"

		// top pole:
		uPatchLodPing.mElements[insertIdxXtra    ].mParams           = vec4(-PI/ 64.0, PI/ 64.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra    ].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);

		uPatchLodPing.mElements[insertIdxXtra + 1].mParams           = vec4(-PI/128.0, PI/128.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra + 1].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);

		uPatchLodPing.mElements[insertIdxXtra + 2].mParams           = vec4(-PI/256.0, PI/256.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra + 2].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);

		// bottom pole:
		uPatchLodPing.mElements[insertIdxXtra + 3].mParams           = vec4(PI-PI/ 64.0, PI+PI/ 64.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra + 3].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);

		uPatchLodPing.mElements[insertIdxXtra + 4].mParams           = vec4(PI-PI/128.0, PI+PI/128.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra + 4].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);

		uPatchLodPing.mElements[insertIdxXtra + 5].mParams           = vec4(PI-PI/256.0, PI+PI/256.0, 3.0*PI/2.0, PI/2.0);
		uPatchLodPing.mElements[insertIdxXtra + 5].mObjectIdUserData = uvec4(objectId, /* Brain dataset dimensions: */ datasetDimensions.x, datasetDimensions.y, /* Brain dataset ID => */ glyphId);
	}
#endif
}
